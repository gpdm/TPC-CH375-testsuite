@ECHO OFF

REM Check if \TEMP\BENCH.RUN checkpoint file exists
REM Assume failure or premature aborting during previous run
REM and skip over the currect benchmark accordingly
IF EXIST \TEMP\BENCH.RUN GOTO DONE

:BOOTLOG
REM add boot log
CD \TEMP\%1
\UTIL\CAPTURE t
REN TEXT.VID DRIVER.LOG 

:ENUMDRVS
SET DRIVE=/
REM NOTE: this implementation is broken in DR DOS
REM FOR %%d in (C D E F G H I J K L M N O P Q R S T U V W X Y Z) DO IF EXIST %%d:\NUL SET DRIVE=%%d
REM old school, but portable ...
IF EXIST C:\NUL SET DRIVE=C
IF EXIST D:\NUL SET DRIVE=D
IF EXIST E:\NUL SET DRIVE=E
IF EXIST F:\NUL SET DRIVE=F
IF EXIST G:\NUL SET DRIVE=G
IF EXIST H:\NUL SET DRIVE=H
IF EXIST I:\NUL SET DRIVE=I
IF EXIST J:\NUL SET DRIVE=J
IF EXIST K:\NUL SET DRIVE=K
IF EXIST L:\NUL SET DRIVE=L
IF EXIST M:\NUL SET DRIVE=M
IF EXIST N:\NUL SET DRIVE=N
IF EXIST O:\NUL SET DRIVE=O
IF EXIST P:\NUL SET DRIVE=P
IF EXIST Q:\NUL SET DRIVE=Q
IF EXIST R:\NUL SET DRIVE=R
IF EXIST S:\NUL SET DRIVE=S
IF EXIST T:\NUL SET DRIVE=T
IF EXIST U:\NUL SET DRIVE=U
IF EXIST V:\NUL SET DRIVE=V
IF EXIST W:\NUL SET DRIVE=W
IF EXIST X:\NUL SET DRIVE=X
IF EXIST Y:\NUL SET DRIVE=Y
IF EXIST Z:\NUL SET DRIVE=Z
IF NOT "%DRIVE%" == "/" GOTO BENCH

:FAIL
ECHO ERROR: Expecting ISA2USB to be available at C: - Z:
ECHO        No drive found (or inaccessible)
ECHO.
ECHO ABORTING now!
GOTO DONE

:BENCH
ECHO Running DiskTest benchmark for "%1" on drive %DRIVE%: ...
ECHO > \TEMP\BENCH.RUN
%DRIVE%:
IF NOT ERRORLEVEL == 0 GOTO FAIL
A:\UTIL\DISKTEST

REM if CHKDSK exists, run it
IF EXIST A:\CHKDSK.EXE GOTO CHECKDISK
IF EXIST A:\CHKDSK.COM GOTO CHECKDISK

GOTO DONE


:CHECKDISK
ECHO Running CHKDSK benchmark drive %DRIVE%: ...
A:\CHKDSK %DRIVE%:


:DONE
A:

REM add benchmark log
CD \TEMP\%1
\UTIL\CAPTURE t
REN TEXT.VID BENCH.LOG 

IF EXIST \TEMP\BENCH.RUN DEL \TEMP\BENCH.RUN
