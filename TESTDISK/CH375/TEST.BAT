@ECHO OFF

IF EXIST \TEMP\COMPLETE     GOTO COMPLETE
IF EXIST \TEMP\RUNBENCH.BAT CALL \TEMP\RUNBENCH.BAT

REM check for previous stages to resume from
IF EXIST \TEMP\CH375V86\NUL GOTO CLEANUP
IF EXIST \TEMP\CH375V88\NUL \CH375\INSTALL.BAT CH375V86
IF EXIST \TEMP\CH375V16\NUL \CH375\INSTALL.BAT CH375V86
IF EXIST \TEMP\CH375216\NUL \CH375\INSTALL.BAT CH375V16 
IF EXIST \TEMP\CH375286\NUL \CH375\INSTALL.BAT CH375216 
IF EXIST \TEMP\CH37520A\NUL \CH375\INSTALL.BAT CH375286
IF EXIST \TEMP\CH37519A\NUL \CH375\INSTALL.BAT CH37520A
IF EXIST \TEMP\CH37519\NUL  \CH375\INSTALL.BAT CH37519A

REM default to begin with if no previous stages exist
\CH375\INSTALL.BAT CH37519

GOTO DONE

:CLEANUP
IF EXIST \CONFIG.SYS        DEL \CONFIG.SYS
IF EXIST \TEMP\RUNBENCH.BAT DEL \TEMP\RUNBENCH.BAT
ECHO > \TEMP\COMPLETE

:COMPLETE
ECHO Benchmarks are completed. No further steps are taken.
ECHO Output logs are located in:
ECHO.
ECHO     \TEMP\CH375*
ECHO.
ECHO If you want to rerun, then remove the stage files like this:
ECHO.
ECHO     DEL \TEMP\COMPLETE 
ECHO.
ECHO  Also remove:
ECHO.
ECHO     \TEMP\CH375*\*.LOG
ECHO     \TEMP\CH375*
ECHO.
ECHO Good bye.
CD \TEMP
GOTO DONE

:DONE
